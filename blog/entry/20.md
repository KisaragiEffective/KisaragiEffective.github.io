# JVMS 4.3.4とBNF
久しぶりにブログを書くような気がする。

## なぜ JVMS?
なんとなく、ASMを使ってジェネリックメソッドをゴニョろうと思ったら、Descriptorが複雑でムカついたから

## Descriptorとは
ただの型よりも強いやつ。型変数の名前とか、ボウンドとかが保存されている。

## ジェネリックメソッドのDescriptor
(§4.3.4から抜粋)
```
ClassTypeSignature:
    L PackageSpecifier_opt SimpleClassTypeSignature ClassTypeSignatureSuffix* ;

PackageSpecifier:
    Identifier / PackageSpecifier*

SimpleClassTypeSignature:
    Identifier TypeArguments_opt

ClassTypeSignatureSuffix:
    . SimpleClassTypeSignature

TypeVariableSignature:
    T Identifier ;

TypeArguments:
    < TypeArgument+ >

TypeArgument:
    WildcardIndicator_opt FieldTypeSignature
    *

WildcardIndicator:
    +
    -

ArrayTypeSignature:
    [ TypeSignature

TypeSignature:
    FieldTypeSignature
    BaseType
```
(※_optで終わるものはオプショナル)

ぬぉあああああああああああああああああ！！！！！！！！！！！！！！

----
* `ClassTypeSignature`: 参照型のパッケージ名をどうのこうのしているやつ。
* `TypeVariableSignature`: 型変数のフォーマット。
* `TypeArgument`: 型引数のフォーマット。`?`は`*`になる。
* `WildcardIndicator`: `? extends`は`+`に、`? super`は`-`になる。
* `ArrayTypeSignature`: 配列型のフォーマット。
* `TypeSignature`: 全ての型のフォーマット。
ここで、IdentifierはフォーマットとしてJVMSに示されていないが、こう書いてある (抜粋):

```
In the following, the terminal symbol Identifier is used to denote the name of a type, field, local variable, parameter, 
method, or type variable,as generated by a Java compiler. Such a name must not contain any of the ASCII characters 
. ; [ / < > : (that is, the characters forbidden in method names (§4.2.2) and also colon) 
but may contain characters that must not appear in an identifier in the Java programming language (JLS §3.8).
```

つまり、`Identifier`は型、フィールド、ローカル変数、引数 (?)、メソッド、型変数の名前であり、正規表現で言うと`/^[^.;\[/<>:]+$/`で表すことができる。
あとは`BaseType`と`VoidDescriptor`の定義を見つける。

### BaseType
§4.3.2 (抜粋):
```
BaseType:
    B
    C
    D
    F
    I
    J
    S
    Z
```

つまり、プリミティブの名前であった。

### VoidDescriptor
なんとなく予想がつくが―。

§4.3.3 (抜粋):

```
VoidDescriptor:
    V
```

やっぱり。

## EBNF
```ebnf
<ClassSignature> ::= <FormalTypeParameters>? <SuperclassSignature> <SuperinterfaceSignature>*
<FormalTypeParameters> ::= "<" <FormalTypeParameter>+ ">"
<FormalTypeParameter> ::= <Identifier> <ClassBound> <InterfaceBound>*
<ClassBound> ::= ":" <FieldTypeSignature>?
<InterfaceBound> ::= ":" <FieldTypeSignature>
<SuperclassSignature> ::= <ClassTypeSignature>
<SuperinterfaceSignature> ::= <ClassTypeSignature>
<FieldTypeSignature> ::= <ClassTypeSignature> | <ArrayTypeSignature> | <TypeVariableSignature>
<ClassTypeSignature> ::= "L" <PackageSpecifier>? <SimpleClassTypeSignature> <ClassTypeSignatureSuffix>* ";"
<PackageSpecifier> ::= <Identifier> "/" <PackageSpecifier>*
<SimpleClassTypeSignature> ::= <Identifier> <TypeArguments>?
<ClassTypeSignatureSuffix> ::= "." <SimpleClassTypeSignature>
<TypeVariableSignature> ::= "T" <Identifier> ";"
<TypeArguments> ::= "<" <TypeArgument>+ ">"
<TypeArgument> ::= <WildcardIndicator>? <FieldTypeSignature> | "*"
<WildcardIndicator> ::= "+" | "-"
<ArrayTypeSignature> ::= "[" <TypeSignature>
<TypeSignature> ::= <FieldTypeSignature> | <BaseType>
<MethodTypeSignature> ::= <FormalTypeParameters>? <TypeSignature>* <ReturnType> <ThrowsSignature>*
<ReturnType> ::= <TypeSignature> | <VoidDescriptor>
<ThrowsSignature> ::= "^" <ClassTypeSignature> | "^" <TypeVariableSignature>
<Identifier> ::= /* ... */
<BaseType> ::= "B" | "C" | "D" | "F" | "I" | "J" | "S" | "Z"
<VoidDescriptor> ::= "V"
```
